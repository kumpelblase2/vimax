buildscript {
	ext {
		kotlinVersion = '1.3.70'
		springBootVersion = '2.1.3.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

plugins {
    id "com.github.node-gradle.node" version "2.1.1"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'de.eternalwings'
version = '1.0'
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
    maven { url "https://dl.bintray.com/hotkeytlt/maven" }
    maven { url "https://dl.bintray.com/s1m0nw1/KtsRunner" }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-batch")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("net.bramp.ffmpeg:ffmpeg:0.6.2")
    implementation("org.flywaydb:flyway-core:5.2.4")
    implementation("com.vladmihalcea:hibernate-types-52:2.4.2")
    implementation("com.github.h0tk3y.betterParse:better-parse-jvm:0.4.0-alpha-3")
    implementation("org.xerial:sqlite-jdbc:3.27.2.1")
    implementation("de.swirtz:ktsRunner:0.0.7")
    implementation("org.jetbrains.kotlin:kotlin-script-runtime:1.3.21")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
	testCompile("org.springframework.boot:spring-boot-starter-test")
}

task buildFrontend(type: NpmTask) {
    args = ["run", "build"]
}

processResources {
    from('src/frontend/dist') {
        into 'public'
    }
}
processResources.dependsOn(buildFrontend)

node {
    npmInstallCommand = "ci"
    nodeModulesDir = file("${project.projectDir}/src/frontend")
}

import com.github.jengelman.gradle.plugins.shadow.transformers.*

shadowJar {
    manifest {
        attributes 'Main-Class': 'de.eternalwings.vima.VimaApplicationKt'
    }

    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
}
